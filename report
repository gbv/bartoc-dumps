#!/usr/bin/env perl
use v5.14;
use Catmandu -all;
use List::Util;

my %missing;
foreach (qw(url license created extent creator scopeNote)) {
    $missing{$_} = exporter('TSV', header => 0, file => "reports/no-$_.tsv");
}

my $labelReport = exporter('TSV', file => 'reports/labels.tsv',
    fields => 'uri,prefLabels,prefUnknown,altLabels,altLanguages,altUnknown,notations',
);
my $languageReport = exporter('TSV', file => 'reports/languages.tsv', fields => 'uri,language', header => 0);

foreach my $file (<scheme/*.json>) {
  importer('JSON', file => $file)->each(sub {
    my $data = shift;
    my $uri = $data->{uri};
    
    # no-....tsv
    foreach (keys %missing) {
        $missing{$_}->add({ uri => $uri }) unless $data->{$_};
    }
 
    # labels.tsv
    $data->{altLabel} //= {};
    $data->{prefLabel} //= {};

    my $labels = {
        uri => $uri,
        prefLabels      => scalar keys %{$data->{prefLabel}},
        prefUnknown     => defined $data->{prefLabel}{und} ? 1 : 0,
        altLabels       => List::Util::sum0( scalar map { @$_ } values %{$data->{altLabel}}),
        altLanguages    => scalar keys %{$data->{altLabel}},
        altUnknown      => scalar @{ $data->{altLabel}{und} // [] },
        notations       => scalar @{ $data->{notation} // [] },
    };

    $labelReport->add($labels);

    # languages.tsv
    foreach (@{$data->{languages} // []}) {
        $languageReport->add({ uri => $uri, language => $_ });
    }
  });
}
