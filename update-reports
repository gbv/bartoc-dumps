#!/usr/bin/env perl
use v5.14;
use Catmandu -all;
use List::Util;

my $noUrl = exporter('TSV', header => 0, file => 'reports/no-url.tsv');
my $labelReport = exporter('TSV', file => 'reports/labels.tsv',
    fields => 'uri,prefLabels,prefUnknown,altLabels,altLanguages,altUnknown,notations',
);
my $languageReport = exporter('TSV', file => 'reports/languages.tsv', fields => 'uri,language', header => 0);

importer('JSON', file => 'bartoc-jskos.json')->each(sub {
    my $data = shift;
    my $uri = $data->{uri};
    
    # no-url.tsv
    $noUrl->add({ uri => $uri }) unless $data->{url};
 
    # labels.tsv
    $data->{altLabel} //= {};
    $data->{prefLabel} //= {};

    my $labels = {
        uri => $uri,
        prefLabels      => scalar keys %{$data->{prefLabel}},
        prefUnknown     => defined $data->{prefLabel}{und} ? 1 : 0,
        altLabels       => List::Util::sum0( scalar map { @$_ } values %{$data->{altLabel}}),
        altLanguages    => scalar keys %{$data->{altLabel}},
        altUnknown      => scalar @{ $data->{altLabel}{und} // [] },
        notations       => scalar @{ $data->{notation} // [] },
    };

    $labelReport->add($labels);

    # languages.tsv
    foreach (@{$data->{languages} // []}) {
        $languageReport->add({ uri => $uri, language => $_ });
    }
});
